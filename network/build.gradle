plugins {
    id 'com.android.library'
    id 'maven-publish'
}

def groupIdStr = "com.whiner.network"
def artifactIdStr = "network"
def versionStr = "1.3"

android {
    namespace 'com.whiner.network'
    compileSdk 33

    defaultConfig {
        minSdk 19
        targetSdk 33

        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildFeatures {
        viewBinding true
    }

    android.libraryVariants.all { variant ->
        // aar 输出文件名配置
        variant.outputs.all { output -> outputFileName = "${groupIdStr}-${artifactIdStr}-${versionStr}.aar"
        }
    }

    publishing {
        singleVariant("release")
    }

    //配置这个并不是让你隐藏过时api，而是让过时api或者不合规矩的语法展现出来
    allprojects {
        gradle.projectsEvaluated {
            tasks.withType(JavaCompile) {
                options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
            }
        }
    }

}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                //#fit Execution optimizations have been disabled for task
                tasks.named("generateMetadataFileForReleasePublication").configure {
                    dependsOn("bundleDebugAar")
                }
                //组包名
                groupId = groupIdStr
                //依赖库名称
                artifactId = artifactIdStr
                //依赖库版本号
                version = versionStr
            }
        }
    }
}

dependencies {

    api project(path: ':common')
    //okhttp-需要混淆
    //noinspection GradleDependency
    api 'com.squareup.okhttp3:okhttp:3.12.13'
    //noinspection GradleDependency
    api 'com.squareup.okhttp3:logging-interceptor:3.12.13'
    //Retrofit2
    //noinspection GradleDependency
    api 'com.squareup.retrofit2:retrofit:2.6.4'
    //noinspection GradleDependency
    api 'com.squareup.retrofit2:converter-gson:2.6.4'
    //noinspection GradleDependency
    api 'com.squareup.retrofit2:converter-scalars:2.6.4'
    //noinspection GradleDependency
    api 'com.squareup.retrofit2:adapter-rxjava2:2.6.4'
    //RxJava-不需要混淆规则
    api 'io.reactivex.rxjava2:rxandroid:2.1.1'
    api 'io.reactivex.rxjava2:rxjava:2.2.21'

}